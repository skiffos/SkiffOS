setenv dtb_name "sun6i-a31s-sinovoip-bpi-m2.dtb"
setenv condev "console=tty1 console=ttyS0,115200n8"
setenv verify 0
setenv bootlogo "false"

setenv devtype mmc
setenv devnum 0
setenv bootpart 1

setenv fdt_addr_r "0x43000000"
setenv kernel_addr_r "0x42000000"
setenv ramdisk_addr_r "0x43400000"

# determine uuid of the boot partition
part uuid ${devtype} ${devnum}:${bootpart} uuid

# use /init script to deferred mount /
setenv bootmem "root=PARTUUID=${uuid} rw rootwait init=/skiff-init/skiff-init-squashfs"

# boot args
setenv bootargs "${bootmem} ${condev} no_console_suspend fsck.repair=yes net.ifnames=0"

# boot from sd card
setenv devnum 0
mmc dev ${devnum}

echo "Loading zImage to ${kernel_addr_r}"
load mmc ${devnum}:${bootpart} ${kernel_addr_r} zImage

echo "Loading dtb to ${fdt_addr_r}"
load mmc ${devnum}:${bootpart} ${fdt_addr_r} ${dtb_name}

echo "Booting with bootargs: ${bootargs}"
bootz ${kernel_addr_r} - ${fdt_addr_r}
